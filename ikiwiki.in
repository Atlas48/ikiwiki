#!/usr/bin/perl -T
$ENV{PATH}="/usr/local/bin:/usr/bin:/bin";
delete @ENV{qw{IFS CDPATH ENV BASH_ENV}};

package IkiWiki;

use warnings;
use strict;
use lib '.'; # For use in nonstandard directory, munged by Makefile.
use IkiWiki;

sub usage () { #{{{
	die gettext("usage: ikiwiki [options] source dest"), "\n";
} #}}}

sub getconfig () { #{{{
	if (! exists $ENV{WRAPPED_OPTIONS}) {
		%config=defaultconfig();
		eval q{use Getopt::Long};
		Getopt::Long::Configure('pass_through');
		GetOptions(
			"setup|s=s" => \$config{setup},
			"wikiname=s" => \$config{wikiname},
			"verbose|v!" => \$config{verbose},
			"syslog!" => \$config{syslog},
			"rebuild!" => \$config{rebuild},
			"refresh!" => \$config{refresh},
			"post-commit" => \$config{post_commit},
			"render=s" => \$config{render},
			"wrappers!" => \$config{wrappers},
			"usedirs!" => \$config{usedirs},
			"prefix-directives!" => \$config{prefix_directives},
			"getctime" => \$config{getctime},
			"numbacklinks=i" => \$config{numbacklinks},
			"rcs=s" => \$config{rcs},
			"no-rcs" => sub { $config{rcs}="" },
			"cgi!" => \$config{cgi},
			"discussion!" => \$config{discussion},
			"w3mmode!" => \$config{w3mmode},
			"url=s" => \$config{url},
			"cgiurl=s" => \$config{cgiurl},
			"historyurl=s" => \$config{historyurl},
			"diffurl=s" => \$config{diffurl},
			"svnpath" => \$config{svnpath},
			"adminemail=s" => \$config{adminemail},
			"timeformat=s" => \$config{timeformat},
			"sslcookie!" => \$config{sslcookie},
			"httpauth!" => \$config{httpauth},
			"userdir=s" => \$config{userdir},
			"htmlext=s" => \$config{htmlext},
			"libdir=s" => \$config{libdir},
			"exclude=s@" => sub {
				push @{$config{wiki_file_prune_regexps}}, $_[1];
			},
			"adminuser=s@" => sub {
				push @{$config{adminuser}}, $_[1]
			},
			"templatedir=s" => sub {
				$config{templatedir}=possibly_foolish_untaint($_[1])
			},
			"underlaydir=s" => sub {
				$config{underlaydir}=possibly_foolish_untaint($_[1])
			},
			"wrapper:s" => sub {
				$config{wrapper}=$_[1] ? possibly_foolish_untaint($_[1]) : "ikiwiki-wrap"
			},
			"wrappermode=i" => sub {
				$config{wrappermode}=possibly_foolish_untaint($_[1])
			},
			"plugin=s@" => sub {
				push @{$config{plugin}}, $_[1];
			},
			"disable-plugin=s@" => sub {
				push @{$config{disable_plugins}}, $_[1];
			},
			"pingurl=s" => sub {
				push @{$config{pingurl}}, $_[1];
			},
			"set=s" => sub {
				my ($var, $val)=split('=', $_[1], 2);
				if (! defined $var || ! defined $val) {
					die gettext("usage: --set var=value"), "\n";
				}
				$config{$var}=$val;
			},
			"version" => sub {
				print "ikiwiki version $IkiWiki::version\n";
				exit;
			},
		) || usage();

		if (! $config{setup} && ! $config{render}) {
			loadplugins();
			usage() unless @ARGV == 2;
			$config{srcdir} = possibly_foolish_untaint(shift @ARGV);
			$config{destdir} = possibly_foolish_untaint(shift @ARGV);
			checkconfig();
		}
	}
	else {
		# wrapper passes a full config structure in the environment
		# variable
		eval possibly_foolish_untaint($ENV{WRAPPED_OPTIONS});
		if ($@) {
			error("WRAPPED_OPTIONS: $@");
		}
		loadplugins();
		checkconfig();
	}
} #}}}

sub main () { #{{{
	getconfig();
	
	if ($config{setup}) {
		require IkiWiki::Setup;
		setup();
	}
	elsif ($config{wrapper}) {
		lockwiki();
		require IkiWiki::Wrapper;
		gen_wrapper();
	}
	elsif ($config{cgi}) {
		require IkiWiki::CGI;
		cgi();
	}
	elsif ($config{render}) {
		require IkiWiki::Render;
		commandline_render();
	}
	elsif ($config{post_commit} && ! commit_hook_enabled()) {
		# do nothing
	}
	else {
		lockwiki();
		loadindex();
		require IkiWiki::Render;
		rcs_update();
		refresh();
		saveindex();
	}
} #}}}

main;
