#!/usr/bin/perl
use warnings;
use strict;
use lib '.'; # For use in nonstandard directory, munged by Makefile.
use IkiWiki;
use IkiWiki::Plugin::comments;

sub usage () {
	die gettext("usage: ikiwiki-comment pagefile"), "\n";
}

sub main {
	my $pagefile=shift || usage ();
	my $dir=get_dir($pagefile);
	my $page=get_page($pagefile);

	IkiWiki::Plugin::comments::checkconfig();
	my $comment_num=1+IkiWiki::Plugin::comments::num_comments($page, $dir);

	chomp(my $content = join('', <STDIN>)) unless -t STDIN;

	my $comment=get_comment(get_username(), $comment_num, $content);

	# This will yield a hash of the comment before it's edited,
	# but that's ok; the date provides sufficient entropy to avoid collisions,
	# and the hash of a comment does not need to match its actual content.
	# Doing it this way avoids needing to move the file to a final
	# location after it's edited.
	my $location=IkiWiki::Plugin::comments::unique_comment_location($page, $comment, $dir)."._comment";

	IkiWiki::writefile($location, $dir, $comment);
	exec_editor("$dir/$location") if -t STDIN;
}

sub get_dir {
	my ($file) = @_;
	my $dir=IkiWiki::dirname($file);
	$dir="." unless length $dir;
	return $dir;
}

sub get_page {
	my ($file) = @_;
	my $page=IkiWiki::basename($file);
	$page=~s/\.[^.]+$// unless -d $file;
	return $page;
}

sub get_username {
	my $username = getpwuid($<);
	$username="" unless defined $username;
	return $username;
}

sub get_comment {
	my ($username, $comment_num, $content) = @_;
	$content = '' unless defined $content;
	my $comment="[[!comment format=mdwn\n";
	$comment.=" username=\"$username\"\n";
	$comment.=" subject=\"\"\"comment $comment_num\"\"\"\n";
	$comment.=" " . IkiWiki::Plugin::comments::commentdate() . "\n";
	$comment.=" content=\"\"\"\n$content\n\"\"\"]]\n";
	return $comment;
}

sub exec_editor {
	my ($file) = @_;

	my @editor="vi";
	if (-x "/usr/bin/editor") {
		@editor="/usr/bin/editor";
	}
	if (exists $ENV{EDITOR}) {
		@editor=split(' ', $ENV{EDITOR});
	}
	if (exists $ENV{VISUAL}) {
	@editor=split(' ', $ENV{VISUAL});
	}
	exec(@editor, $file);
}

main(@ARGV);
